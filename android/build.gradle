android {
    buildToolsVersion "28.0.3"
    compileSdkVersion 28
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            java.excludes = ['androidTest/**']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            jniLibs.srcDirs = ['libs']
        }
        mathgame {
            assets.srcDirs = ['assets/tournament_resources/implementations/math', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
        mathgamePro {
            assets.srcDirs = ['assets/tournament_resources/implementations/math', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
        balloon {
            assets.srcDirs = ['assets/tournament_resources/implementations/balloon', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
        balloonPro {
            assets.srcDirs = ['assets/tournament_resources/implementations/balloon', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
        memory {
            assets.srcDirs = ['assets/tournament_resources/implementations/memory', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
        memoryPro {
            assets.srcDirs = ['assets/tournament_resources/implementations/memory', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
        resourcewars {
            assets.srcDirs = ['assets/tournament_resources/implementations/resourcewars', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
        resourcewarsPro {
            assets.srcDirs = ['assets/tournament_resources/implementations/resourcewars', 'assets/tournament_resources/main', 'assets/main_resources/main']
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    configurations {
        all {
            exclude module: 'httpclient'
            exclude module: 'commons-logging'
        }
    }
    packagingOptions {
        exclude 'META-INF/robovm/ios/robovm.xml'
    }
    defaultConfig {
        applicationId "com.habapps"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "game"
    productFlavors {
/////////////////////////////////////////////////////////////////////////////
        mathgame {
            applicationIdSuffix ".mathgame"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/mathgame_icon"]
        }
/////////////////////////////////////////////////////////////////////////////
        mathgamePro {
            applicationIdSuffix ".mathgamepro"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/mathgamepro_icon"]
        }
/////////////////////////////////////////////////////////////////////////////
        balloon {
            applicationIdSuffix ".balloon"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/balloon_icon"]
        }
/////////////////////////////////////////////////////////////////////////////
        balloonPro {
            applicationIdSuffix ".balloonpro"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/balloonpro_icon"]
        }
/////////////////////////////////////////////////////////////////////////////
        memory {
            applicationIdSuffix ".memory"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/memory_icon"]
        }
/////////////////////////////////////////////////////////////////////////////
        memoryPro {
            applicationIdSuffix ".memorypro"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/memorypro_icon"]
        }
/////////////////////////////////////////////////////////////////////////////
        resourcewars {
            applicationIdSuffix ".resourcewars"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/resourcewars_icon"]
        }
/////////////////////////////////////////////////////////////////////////////
        resourcewarsPro {
            applicationIdSuffix ".resourcewarspro"
            versionCode 1
            versionName "1.00"
            dimension "game"
            manifestPlaceholders = [appIcon: "@drawable/resourcewarspro_icon"]
        }
    }
/////////////////////////////////////////////////////////////////////////////

}

// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
task copyAndroidNatives {
    doFirst {
        file("libs/armeabi/").mkdirs()
        file("libs/armeabi-v7a/").mkdirs()
        file("libs/arm64-v8a/").mkdirs()
        file("libs/x86_64/").mkdirs()
        file("libs/x86/").mkdirs()

        configurations.natives.files.each { jar ->
            def outputDir = null
            if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
            if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
            if (jar.name.endsWith("natives-armeabi.jar")) outputDir = file("libs/armeabi")
            if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
            if (jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
            if (outputDir != null) {
                copy {
                    from zipTree(jar)
                    into outputDir
                    include "*.so"
                }
            }
        }
    }
}

tasks.whenTaskAdded { packageTask ->
    if (packageTask.name.contains("package")) {
        packageTask.dependsOn 'copyAndroidNatives'
    }
}

task run(type: Exec) {
    def path
    def localProperties = project.file("../local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }

    def adb = path + "/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.habapps/com.habapps.AndroidLauncher'
}

// sets up the Android Eclipse project, using the old Ant based build.
eclipse {
    // need to specify Java source sets explicitly, SpringSource Gradle Eclipse plugin
    // ignores any nodes added in classpath.file.withXml
    sourceSets {
        main {
            java.srcDirs "src", 'gen'
        }
    }

    jdt {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    classpath {
        plusConfigurations += [project.configurations.compile]
        containers 'com.android.ide.eclipse.adt.ANDROID_FRAMEWORK', 'com.android.ide.eclipse.adt.LIBRARIES'
    }

    project {
        name = appName + "-android"
        natures 'com.android.ide.eclipse.adt.AndroidNature'
        buildCommands.clear();
        buildCommand "com.android.ide.eclipse.adt.ResourceManagerBuilder"
        buildCommand "com.android.ide.eclipse.adt.PreCompilerBuilder"
        buildCommand "org.eclipse.jdt.core.javabuilder"
        buildCommand "com.android.ide.eclipse.adt.ApkBuilder"
    }
}

// sets up the Android Idea project, using the old Ant based build.
idea {
    module {
        sourceDirs += file("src");
        scopes = [COMPILE: [plus: [project.configurations.compile]]]

        iml {
            withXml {
                def node = it.asNode()
                def builder = NodeBuilder.newInstance();
                builder.current = node;
                builder.component(name: "FacetManager") {
                    facet(type: "android", name: "Android") {
                        configuration {
                            option(name: "UPDATE_PROPERTY_FILES", value: "true")
                        }
                    }
                }
            }
        }
    }
}
apply plugin: 'com.google.gms.google-services'